/*
 * This program and the accompanying materials are made available under the terms of the
 * Eclipse Public License v2.0 which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Copyright IBA Group 2023
 */

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'org.jetbrains.intellij' version '1.13.0'
    id 'java'
    id 'org.jetbrains.grammarkit' version '2021.2.2'
}

group 'org.zowe'
version '0.1.0-223'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2022.3'
    //type = 'IC'

    // To have a dependency on zowe explorer from the marketplace
    plugins = ['org.zowe.explorer:1.1.0-223']

    // To have a dependency on built-in plugin from \Project_dir\libs\for-mainframe
    //plugins = ["${projectDir}\\libs\\for-mainframe"]
}

patchPluginXml {
    sinceBuild.set("223.5080")
    untilBuild.set("223.*")
    changeNotes.set("""
      <b>New features:</b>
      <ul>
        <li>Separated the plugin to three versionsNone at the moment</li>
      </ul>
      <br>
      <b>Fixed bugs:</b>
      <ul>
        <li>None at the moment</li>
      </ul>
    """)
}

test {
    // useJUnitPlatform()
    // see https://youtrack.jetbrains.com/issue/IDEA-278926
    scanForTestClasses false
    include "**/*Test*"
}

sourceSets {
    main {
        java.srcDirs("src/main")
        kotlin.srcDirs("src/main")
    }
    test {
        java.srcDirs("src/test")
        kotlin.srcDirs("src/test")
    }
}

grammarKit {
    // version of IntelliJ patched JFlex - https://github.com/JetBrains/intellij-deps-jflex
    jflexRelease = '1.7.0-1'
    // release version of Grammar-Kit - https://github.com/JetBrains/Grammar-Kit
    grammarKitRelease = System.getenv().getOrDefault('GRAMMAR_KIT_VERSION', '2021.1.2')
}

generateParser {
    source = 'src/main/kotlin/org/zowe/jcl/lang/Jcl.bnf'
    targetRoot = 'src/main/java'
    pathToParser = '/org/zowe/jcl/lang/parser/JclParser.java'
    pathToPsiRoot = '/org/zowe/jcl/lang/psi'
    purgeOldFiles = true
}

generateLexer {
    source = 'src/main/kotlin/org/zowe/jcl/lang/Jcl.flex'
    targetDir = 'src/main/java/org/zowe/jcl/lang'
    targetClass = 'JclLexer'
    purgeOldFiles = true
}

compileKotlin {
    dependsOn(generateLexer, generateParser)
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()
}

compileTestKotlin {
    dependsOn compileKotlin
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()
}

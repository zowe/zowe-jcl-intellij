plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.10'
    id 'org.jetbrains.intellij' version '1.13.0'
    id 'java'
    id 'org.jetbrains.grammarkit' version '2021.2.2'
}

group 'eu.ibagroup'
version '0.1'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2022.1'
    //type = 'IC'

    // To have a dependency on for-mainframe from the marketplace
    plugins = ['eu.ibagroup.formainframe:0.7.1']

    // To have a dependency on built-in plugin from \Project_dir\libs\for-mainframe
    //plugins = ["${projectDir}\\libs\\for-mainframe"]
}

patchPluginXml {
    sinceBuild.set("221.5080")
    untilBuild.set("223.*")
    changeNotes.set("""
      Add change notes here.<br>
      <em>most HTML tags may be used</em>""")
}

test {
    //useJUnitPlatform()
    // see https://youtrack.jetbrains.com/issue/IDEA-278926
    scanForTestClasses false
    include "**/*Test*"
}

sourceSets {
    main {
        java.srcDirs("src/main")
        kotlin.srcDirs("src/main")
    }
    test {
        java.srcDirs("src/test")
        kotlin.srcDirs("src/test")
    }
}

grammarKit {
    // version of IntelliJ patched JFlex - https://mvnrepository.com/artifact/org.jetbrains.intellij.deps.jflex/jflex
    jflexRelease = '1.7.0-1'
    // tag or short commit hash of Grammar-Kit to use - https://github.com/JetBrains/Grammar-Kit
    grammarKitRelease = System.getenv().getOrDefault('GRAMMAR_KIT_VERSION', '2021.1.2')
}

tasks.named("generateParser").configure {
    source = "src/main/kotlin/eu/ibagroup/jcl/lang/Jcl.bnf"
    targetRoot = 'src/main/java'
    pathToParser = '/eu/ibagroup/jcl/lang/parser/JclParser.java'
    pathToPsiRoot = '/eu/ibagroup/jcl/lang/psi'
    purgeOldFiles = true
}

tasks.named("generateLexer").configure {
    dependsOn generateParser

    source = 'src/main/kotlin/eu/ibagroup/jcl/lang/Jcl.flex'
    targetDir = 'src/main/java/eu/ibagroup/jcl/lang/'
    targetClass = 'JclLexer'
    purgeOldFiles = true
}

compileKotlin {
    dependsOn generateLexer
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()
}

compileTestKotlin {
    dependsOn compileKotlin
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()
}